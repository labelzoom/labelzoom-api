plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'io.freefair.lombok'
}

group = 'com.labelzoom.api'
version = '1.3.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    testImplementation platform('org.junit:junit-bom:5.11.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.reflections:reflections:0.10.2'
    testImplementation 'uk.co.jemos.podam:podam:8.0.2.RELEASE'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'labelzoom-api'
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/labelzoom/labelzoom-api'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

// Code coverage
test {
    finalizedBy jacocoTestReport // generate the report after tests run
}
jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}
